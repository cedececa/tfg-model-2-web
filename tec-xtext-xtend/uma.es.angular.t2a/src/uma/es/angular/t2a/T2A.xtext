
grammar uma.es.angular.t2a.T2A with org.eclipse.xtext.common.Terminals
generate t2A "http://www.es.uma/angular/t2a/T2A"

Root:
	'AppName:' appName=STRING
	
	(globalStyle=StyleGlobal)?
	(styleOnline=StyleOnline)?
	(jsOnline=JSOnline)?
	elements+=Element+
;

Element:
	Page | EDOM;

Page:
	'Page' name=ID 
	'{'
	('sclass' '{'(hostclasses+=[StyleClass])* '}')?	
	(home?='home')? 
	(showInNav?='showInNav')?
	(pageFeatures+=PageFeature)*
	'}';

EDOM:
	DOM | Comp | StyleClass ;

StyleGlobal:
	{StyleGlobal} 'StyleGlobal' '{'
		(styleclasses+=StyleClass)*
	'}'
;
StyleOnline:
	{StyleOnline} 'StyleOnline' '{'
		(fullTags+=STRING)*
	'}'
;
JSOnline:
	{JSOnline} 'JSOnline' '{'
		(fullTags+=STRING)*
	'}'
;


StyleClass:
	'StyleClass' name=Name
	'{'
		(sattributes+=SAttributeAndValue';')*
		('after' '{'
			(sattributesAfter+=SAttributeAndValue';')*		
		'}')?
		('active' '{'
			(sattributesActive+=SAttributeAndValue';')*		
		'}')?
		
	'}'
;

SAttributeAndValue:
	stname=Name value=STRING2
;

DOM:
	'Dom' name=ID
	('{' 
	('sclass' '{'(sclasses+=[StyleClass])* '}')?	
		(features+=Feature)*
	'}')?;
	
Comp:
	'Comp' name=ID
	(
	'{'
	('sclass' '{'(hostclasses+=[StyleClass])* '}')?	
		(features+=Feature)*
	'}'	
	)?
;

InstanciaEDOM: instancia=[EDOM] ('{' 
		('sclass' '{'(sclasses+=[StyleClass])* '}')?
		(insfeatures+=InstanceEDOMFeature)*
	'}'
)?; 

InstanceEDOMFeature: 
	instanciaEDOM=InstanciaEDOM  | string=STRING;
	
PageFeature: 
	instanciaEDOM=InstanciaEDOM | 
	string=STRING ; 

Feature: 
	instanciaEDOM=InstanciaEDOM |
	AllowSlot?='AllowSlot' |
    string=STRING
; 

Name:
	 ('_'|'#'|'-'|'*'|'.'|'::'|'>'|ID)*
;

@Override 
terminal STRING:
    '"' ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
    "'" ( '\\'('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'";
   
terminal STRING2: 
    (': '|':'|' : ')('a'..'z'|'A'..'Z'|'0'..'9'|'_'|'#'|'-'|'%'|'*'|','|', '|'('|')'|'px '|'"') ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|'#'|'-'|'%'|'*'|','|', '|'('|')'|'px '|'"')*;

    